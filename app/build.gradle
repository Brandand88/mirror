apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
    buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")

    defaultConfig {
        applicationId "com.vaporwarecorp.mirror"
        minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")
        versionCode globalConfiguration.getAt("androidVersionCode")
        versionName globalConfiguration.getAt("androidVersionName")
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    lintOptions {
        abortOnError false
        lintConfig rootProject.file('lint.xml')
    }
    signingConfigs {
        // You must set up an environment var before release signing
        // Run: export APP_KEY={password}
        // TODO Add your release keystore in /keystore folder
        release {
            storeFile file('keystore/release.keystore')
            keyAlias 'alias'
            storePassword "$System.env.APP_KEY"
            keyPassword "$System.env.APP_KEY"
        }
        debug {
            storeFile file('keystore/debug.keystore')
            keyAlias 'androiddebugkey'
            storePassword 'android'
            keyPassword 'android'
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {

    final EVENTBUS_VERSION = '3.0.0'
    final HOUNDIFY_VERSION = '0.2.15'
    final LEAKCANARY_VERSION = '1.4-beta1'
    final PLAY_VERSION = '9.2.0'
    final PROXIMITY_KIT_VERSION = '0.10.2'
    final MULTIDEX_VERSION = '1.0.1'
    final RETROFIT_VERSION = '2.0.2'
    final ROBOPUPU_VERSION = '0.5.0'
    final SPOTIFY_VERSION = '1.0.0-beta12'
    final SUPPORT_LIBRARY_VERSION = '24.0.0'
    final NANOHTTP_VERSION = '2.3.0'
    final TIMBER_VERSION = '4.1.1'
    final JUNIT_VERSION = '4.12'

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:palette-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:multidex:$MULTIDEX_VERSION"

    compile "com.google.android.gms:play-services-maps:$PLAY_VERSION"
    compile "com.google.firebase:firebase-core:$PLAY_VERSION"
    compile "com.google.firebase:firebase-crash:$PLAY_VERSION"
    compile 'com.github.jd-alexander:library:1.1.0'

    compile "com.robopupu:robopupu:$ROBOPUPU_VERSION"
    apt "com.robopupu:robopupu-compiler:$ROBOPUPU_VERSION"
    apt 'com.squareup:javapoet:1.6.1'

    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.5'

    compile "org.nanohttpd:nanohttpd:$NANOHTTP_VERSION"

    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"

    compile "info.android15.solid:streams:2.0.3"
    compile "info.android15.solid:collections:2.0.3"

    compile "org.greenrobot:eventbus:$EVENTBUS_VERSION"

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp-integration:1.4.0'

    compile "com.jakewharton.timber:timber:$TIMBER_VERSION"

    compile "com.radiusnetworks.proximity:proximitykit-android:$PROXIMITY_KIT_VERSION@aar"

    compile("hound.android:hound-sdk:$HOUNDIFY_VERSION@aar") { transitive = true }

    compile "com.spotify.sdk:spotify-auth:$SPOTIFY_VERSION@aar"
    compile "com.spotify.sdk:spotify-player:$SPOTIFY_VERSION@aar"
    compile 'com.github.kaaes:spotify-web-api-android:0.3.0'

    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.daimajia.easing:library:1.0.1@aar'
    compile 'com.daimajia.androidanimations:library:1.1.3@aar'

    compile 'com.github.iammert:MusicPlayerView:e3b937c729'

    compile 'com.twilio:conversations-android:0.12.1'
    compile('com.twilio.sdk:twilio-java-sdk:5.10.1') {
        exclude group: 'org.codehaus.jackson'
        exclude group: 'io.jsonwebtoken'
    }
    compile 'io.jsonwebtoken:jjwt:0.6.0'
    compile 'cn.pedant.sweetalert:library:1.3'

    compile 'com.pixplicity.easyprefs:library:1.8.1@aar'

    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-io:commons-io:2.4'

    testCompile "junit:junit:$JUNIT_VERSION"

    debugCompile "com.squareup.leakcanary:leakcanary-android:$LEAKCANARY_VERSION"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LEAKCANARY_VERSION"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LEAKCANARY_VERSION"

}

ant.importBuild 'assets.xml'
preBuild.dependsOn(list, checksum)
clean.dependsOn(clean_assets)

apply plugin: 'com.google.gms.google-services'