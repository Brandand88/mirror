apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.vaporwarecorp.mirror"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    signingConfigs {
        // You must set up an environment var before release signing
        // Run: export APP_KEY={password}
        // TODO Add your release keystore in /keystore folder
        release {
            storeFile file('keystore/release.keystore')
            keyAlias 'alias'
            storePassword "$System.env.APP_KEY"
            keyPassword "$System.env.APP_KEY"
        }
        debug {
            storeFile file('keystore/debug.keystore')
            keyAlias 'androiddebugkey'
            storePassword 'android'
            keyPassword 'android'
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a'
            universalApk false
        }
    }
}

dependencies {

    final EVENTBUS_VERSION = '3.0.0'
    final IJKPLAYER_VERSION = '0.4.5.1'
    final LEAKCANARY_VERSION = '1.4-beta1'
    final MULTIDEX_VERSION = '1.0.1'
    final PLAY_VERSION = '8.4.0'
    final SPOTIFY_VERSION = '1.0.0-beta12'
    final SUPPORT_LIBRARY_VERSION = '23.3.0'
    final TIMBER_VERSION = '4.1.1'
    final JUNIT_VERSION = '4.12'


    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.google.android.gms:play-services-maps:$PLAY_VERSION"
    compile 'com.github.jd-alexander:library:1.1.0'

    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:leanback-v17:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:palette-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:multidex:$MULTIDEX_VERSION"

    compile "org.greenrobot:eventbus:$EVENTBUS_VERSION"

    compile "tv.danmaku.ijk.media:ijkplayer-java:$IJKPLAYER_VERSION"
    compile "tv.danmaku.ijk.media:ijkplayer-armv7a:$IJKPLAYER_VERSION"

    compile 'com.squareup.retrofit2:retrofit:2.0.1'
    compile 'com.squareup.retrofit2:converter-gson:2.0.1'

    compile "com.jakewharton.timber:timber:$TIMBER_VERSION"

    compile('com.radiusnetworks.proximity:proximitykit-android:0.10.2@aar') { transitive = true }

    compile('hound.android:hound-sdk:0.2.15@aar') { transitive = true }

    compile "com.spotify.sdk:spotify-auth:$SPOTIFY_VERSION@aar"
    compile "com.spotify.sdk:spotify-player:$SPOTIFY_VERSION@aar"
    compile 'com.github.kaaes:spotify-web-api-android:0.3.0'

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp-integration:1.4.0'

    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-io:commons-io:2.4'

    compile 'com.github.florent37:glidepalette:1.0.6@aar'
    compile 'com.github.florent37:viewanimator:1.0.3@aar'
    compile 'com.nineoldandroids:library:2.4.0'

    compile project(':libs:OpenCV')
    compile project(':libs:TouchFree')

    testCompile "junit:junit:$JUNIT_VERSION"

    debugCompile "com.squareup.leakcanary:leakcanary-android:$LEAKCANARY_VERSION"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LEAKCANARY_VERSION"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LEAKCANARY_VERSION"
}

ant.importBuild 'assets.xml'
preBuild.dependsOn(list, checksum)
clean.dependsOn(clean_assets)
