apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "com.vaporwarecorp.mirror"
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 1
        versionName "1.0.0"
        multiDexEnabled true
        ndk {
            abiFilter "armeabi-v7a"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile file('keystore/release.jks')
            keyAlias "$System.env.APP_KEY_ALIAS"
            storePassword "$System.env.APP_STORE_PASSWORD"
            keyPassword "$System.env.APP_KEY_PASSWORD"
        }
        debug {
            storeFile file('keystore/debug.jks')
            keyAlias 'androiddebugkey'
            storePassword 'android'
            keyPassword 'android'
        }
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
    }
    lintOptions {
        abortOnError false
        lintConfig rootProject.file('lint.xml')
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {
    final EVENTBUS_VERSION = '3.0.0'
    final HOUNDIFY_VERSION = '0.2.19'
    final LEAKCANARY_VERSION = '1.4'
    final PLAY_VERSION = '9.6.1'
    final PROXIMITY_KIT_VERSION = '0.12.1'
    final MULTIDEX_VERSION = '1.0.1'
    final RETROFIT_VERSION = '2.0.2'
    final ROBOPUPU_VERSION = '0.5.0'
    final SUPPORT_LIBRARY_VERSION = '24.2.1'
    final NANOHTTP_VERSION = '2.3.1'
    final TIMBER_VERSION = '4.1.1'
    final JUNIT_VERSION = '4.12'

    compile fileTree(dir: 'libs', include: ['*.jar'])

    //compile "com.android.support:support-compat:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:multidex:$MULTIDEX_VERSION"

    compile "com.google.android.gms:play-services-maps:$PLAY_VERSION"
    compile 'com.github.jd-alexander:library:1.1.0'
    releaseCompile "com.google.firebase:firebase-core:$PLAY_VERSION"
    releaseCompile "com.google.firebase:firebase-crash:$PLAY_VERSION"

    compile "com.robopupu:robopupu:$ROBOPUPU_VERSION"
    apt "com.robopupu:robopupu-compiler:$ROBOPUPU_VERSION"
    apt 'com.squareup:javapoet:1.6.1'

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.0'

    compile "org.nanohttpd:nanohttpd:$NANOHTTP_VERSION"
    compile("org.nanohttpd:nanohttpd-apache-fileupload:$NANOHTTP_VERSION") {
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'commons-fileupload'
    }
    compile 'javax.servlet:servlet-api:2.5'
    compile 'com.sromku:simple-storage:1.2.0'

    compile("com.squareup.retrofit2:retrofit:$RETROFIT_VERSION") {
        exclude group: 'com.squareup.okhttp3'
    }
    compile "com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"

    compile "info.android15.solid:streams:2.0.3"
    compile "info.android15.solid:collections:2.0.3"

    compile "org.greenrobot:eventbus:$EVENTBUS_VERSION"

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp-integration:1.4.0'

    compile "com.jakewharton.timber:timber:$TIMBER_VERSION"

    compile "com.radiusnetworks.proximity:proximitykit-android:$PROXIMITY_KIT_VERSION@aar"

    compile("hound.android:hound-sdk:$HOUNDIFY_VERSION@aar") { transitive = true }

    compile "com.spotify.sdk:spotify-auth-beta22-noconnect-2.20b@aar"
    compile "com.spotify.sdk:spotify-player-beta22-noconnect-2.20b@aar"
    compile 'com.github.kaaes:spotify-web-api-android:0.3.0'

    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.daimajia.easing:library:1.0.1@aar'
    compile 'com.daimajia.androidanimations:library:1.1.3@aar'

    compile 'com.github.iammert:MusicPlayerView:e3b937c729'

    compile 'com.twilio:conversations-android:0.12.2'
    compile('com.twilio.sdk:twilio-java-sdk:5.11.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'org.codehaus.jackson'
        exclude group: 'io.jsonwebtoken'
    }
    compile 'io.jsonwebtoken:jjwt:0.6.0'
    compile 'cn.pedant.sweetalert:library:1.3'

    compile 'cloud.artik:artikcloud-java:2.0.5'
    compile 'com.google.api-client:google-api-client-android:1.20.0'

    compile 'com.pixplicity.easyprefs:library:1.8.1@aar'

    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-io:commons-io:2.4'

    compile('com.willblaschko.android.alexa:AlexaAndroid:2.1.4-alpha') {
        exclude group: 'commons-fileupload'
    }
    compile 'com.github.Kaljurand:speechutils:3cca49c288@aar'

    testCompile "junit:junit:$JUNIT_VERSION"

    debugCompile "com.squareup.leakcanary:leakcanary-android:$LEAKCANARY_VERSION"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LEAKCANARY_VERSION"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LEAKCANARY_VERSION"

}

ant.importBuild 'assets.xml'
preBuild.dependsOn(list, checksum)
clean.dependsOn(clean_assets)

apply plugin: 'com.google.gms.google-services'